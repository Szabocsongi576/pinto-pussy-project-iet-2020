apply plugin: "idea"
apply plugin: "java"
apply plugin: "maven"

wrapper {
	gradleVersion = '6.4'
	distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

sourceSets {
	main {
		java {
			srcDir 'main/src'
		}
		resources {
			srcDir 'main/resources'
		}
	}
	test {
		java {
			srcDir 'test/src'
		}
		resources {
			srcDir 'test/resources'
		}
	}
}

group = "com.complexible.pinto"
version = "2.0.1-SNAPSHOT"

ext {
	projectDescription = "Pinto: A lightweight framework for mapping Java Beans into RDF and back again"
	projectUrl = "https://github.com/complexible/pinto"
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
	mavenCentral()
	maven {
		url "http://maven.stardog.com"
	}
}

dependencies {
	compile (group: "com.google.guava", name: "guava", version: "18.0") {
		exclude group: "com.google.code.findbugs"
	}

	compile "commons-beanutils:commons-beanutils:1.9.2"
	compile "commons-codec:commons-codec:1.10"

	compile "com.complexible.common:cp-openrdf-utils:4.0.2"
	compile ("com.complexible.common:cp-common-utils:5.0.1") {
		exclude group: "org.slf4j"
	}

	// these should be coming from cp-openrdf-utils, but it's missing these
	compile "org.openrdf.sesame:sesame-rio-ntriples:4.0.0"

	compile "org.slf4j:slf4j-api:1.7.7"
	compile "org.slf4j:slf4j-jdk14:1.7.7"

	testCompile 'junit:junit:4.11'

	testImplementation 'io.cucumber:cucumber-java8:5.6.0'
	testImplementation 'io.cucumber:cucumber-junit:5.6.0'
}

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}
}

task cucumber() {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = ['--plugin', 'pretty', '--glue', 'com.complexible.pinto', 'test/src/com/complexible/pinto/CucumberTests.java']
		}
	}
}

tasks.withType(Test) {
	systemProperties = System.getProperties()
}
